buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

plugins {
    id "com.github.triplet.play" version "3.7.0"
    id "app.brant.amazonappstorepublisher" version "0.1.0"
}

def keystorePropertiesFile = rootProject.file("keystore.properties");
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def sentryPropertiesFile = rootProject.file("sentry.properties");
def sentryProperties = new Properties()
if (sentryPropertiesFile.exists()) sentryProperties.load(new FileInputStream(sentryPropertiesFile))

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
        // check tasks to add via ./gradlew tasks --all | grep DuplicateClasses
    }
}

static def getDate() {
    // we increment version number every 1000 seconds,
    // we started on 31/08/2016, and introduced splits on 06/02/2020
    // update 2022/11/25 to reset getDate offset (- 88364898L)
    def date = new Date().getTime() / 1000L - 1472637600L - 108380000L - 88364898L;
    return (int) (date / 1000L)
}

static def getFormattedDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd.HHmm')
    return formattedDate
}

apply {
    plugin("com.android.application")
    plugin("com.github.triplet.play")
}

import com.github.triplet.gradle.androidpublisher.ReleaseStatus

play {
    serviceAccountCredentials.set(file("nova.json"))
    track.set("internal")  // rollout, beta, alpha, release, internal
    //userFraction.set(0.1) // rollout rate in %/100
    //releaseStatus.set(ReleaseStatus.IN_PROGRESS)
    releaseStatus.set(ReleaseStatus.COMPLETED)
}

amazon {
    securityProfile = file("nova-amazon.json")
    applicationId = "amzn1.devportal.mobileapp.c59461247b6a4806b4f30cd80076133e"
    pathToApks = [ file("build/outputs/apk/amazon/release/Video-amazon-universal-API_21+-release.apk") ]
    replaceEdit = true
}

ext.abiCodes = ['universal': 0, 'armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]

android {
    namespace 'com.archos.mediacenter.video'
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
            enableV3Signing = true
            enableV4Signing = true
        }
        debug {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
            enableV3Signing = true
            enableV4Signing = true
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    compileSdkVersion 33
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    defaultConfig {
        generatedDensities = []
        minSdkVersion 21
        targetSdkVersion 33
        multiDexEnabled true
        // below version tracks the one from F-Droid or universal package when generated with ./gradlew -Puniversal
        // this is overridden when using split apk and normal split apk generation or oneapk amazon generation
        // amazon apk is generated with ./gradlew -Poneapk
        def novaVersionName = '6.0.89'
        def novaVersionCode = 600089
        if (project.hasProperty("universal")) {
            versionName = novaVersionName
            versionCode = novaVersionCode
        } else {
            // override versionName and versionCode to get it dependent on the date
            versionName = novaVersionName + '-' + getFormattedDate()
            // 2549909 is 20221124 last Google play release to reset counter
            versionCode = 2549909 + getDate()
        }
        if (keystorePropertiesFile.exists()) signingConfig signingConfigs.release
        applicationId "org.courville.nova"
        if (project.hasProperty("sponsor")) {
            buildConfigField "boolean", "ENABLE_SPONSOR", "true"
        } else {
            buildConfigField "boolean", "ENABLE_SPONSOR", "false"
        }
        if (project.hasProperty("adultScrape")) {
            buildConfigField "boolean", "ADULT_SCRAPE", "true"
        } else {
            buildConfigField "boolean", "ADULT_SCRAPE", "false"
        }
        if (sentryPropertiesFile.exists()) {
            if (project.hasProperty("bugReport")) {
                buildConfigField "boolean", "ENABLE_BUG_REPORT", "true"
                buildConfigField "String", "SENTRY_DSN", "\"${sentryProperties['dsn']}\""
            } else {
                buildConfigField "boolean", "ENABLE_BUG_REPORT", "false"
                buildConfigField "String", "SENTRY_DSN", "\"\""
            }
        } else {
            buildConfigField "boolean", "ENABLE_BUG_REPORT", "false"
            buildConfigField "String", "SENTRY_DSN", "\"\""
        }
        resValue "string", "VERSION_NAME", versionName
        resValue "string", "APPLICATION_ID", applicationId
        resValue "string", "APP_INFO", "Nova v" + versionName + " (" + applicationId + ")"
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
        storeArchive {
            enable = false
        }
    }
    splits {
        abi {
            if (project.hasProperty("universal")) {
                // universal if for fdroid now with specific version(Name|Code) and oneapk for amazon
                enable false
                universalApk true
            } else if (project.hasProperty("oneapk")) {
                // universal if for fdroid now with specific version(Name|Code) and oneapk for amazon
                enable true
                universalApk true
            } else {
                enable true
                universalApk false
            }
        }
    }
    flavorDimensions "amazon"
    productFlavors {
        amazon {
            dimension "amazon"
        }
        noamazon {
            dimension "amazon"
        }
        manageexternalstorage {
            dimension "amazon"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/services/**',
                         'com/**',
                         'jcifs2/**',
                         'kotlin/**',
                         'okhttp3/**',
                         'org/**']
            pickFirsts += ['lib/x86/libnvpnativehelper.so',
                           'lib/x86_64/libnvpnativehelper.so',
                           'lib/armeabi-v7a/libnvpnativehelper.so',
                           'lib/arm64-v8a/libnvpnativehelper.so',
                           'lib/x86/libc++_shared.so',
                           'lib/x86_64/libc++_shared.so',
                           'lib/armeabi-v7a/libc++_shared.so',
                           'lib/arm64-v8a/libc++_shared.so']
        }
        resources {
            excludes += ['META-INF/*.kotlin_module',
                         'META-INF/*.version',
                         'META-INF/DEPENDENCIES',
                         'META-INF/LICENSE',
                         'META-INF/NOTICE',
                         'META-INF/LICENSE.txt',
                         'META-INF/NOTICE.txt',
                         'META-INF/beans.xml',
                         'META-INF/maven',
                         'META-INF/services/**',
                         'README',
                         'com/**',
                         'jcifs2/**',
                         'kotlin/**',
                         'okhttp3/**',
                         'org/**',
                         '**/*.txt',
                         '**/*.xml']
        }
    }
    androidResources {
        additionalParameters '--no-version-vectors'
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def api = defaultConfig.minSdkVersion.apiLevel
            if (project.hasProperty("universal")) { // only one apk
                outputFileName = outputFileName.replace("-release", "-universal-release")
            }
            outputFileName = outputFileName.replace("-noamazon-", "-")
                    .replace("-manageexternalstorage-", "-")
                    .replace("-release.apk", "-API_${api}+-release.apk")
                    .replace("-debug.apk", "-API_${api}+-debug.apk")
            variant.getAssembleProvider().configure() {
                it.doLast {
                    println("generated: " + outputFileName + " (" + versionName + ", " + versionCode + ")");
                }
            }
        }
    }
}

dependencies {
    implementation project(':FileCoreLibrary')
    implementation project(':MediaLib')
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.preference:preference:1.2.0'
    // needed to avoid duplicate class issue
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.leanback:leanback:1.2.0-alpha02'
    implementation 'androidx.leanback:leanback-preference:1.2.0-alpha02'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    implementation 'fr.turri:aXMLRPC:1.12.0'
    implementation 'io.sentry:sentry-android:6.8.0'
    implementation 'io.sentry:sentry:6.9.2'
}
